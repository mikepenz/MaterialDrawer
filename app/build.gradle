apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

if (getSigningFile() != null) {
    apply from: "${getSigningFile()}"
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        multiDexEnabled true

        setProperty("archivesBaseName", "MaterialDrawer-v$versionName-c$versionCode")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ignored) {
            }
            minifyEnabled false
        }
        release {
            if (getSigningFile() != null) {
                signingConfig signingConfigs.release
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(':library')
    implementation project(':library-iconics')
    implementation project(':library-nav')

    implementation "androidx.multidex:multidex:${versions.multiDex}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.cardview:cardview:${versions.cardview}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.slidingpanelayout:slidingpanelayout:${versions.slidingpaneLayout}"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:${versions.swiperefreshLayout}"

    // used to showcase how to load images
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"

    // needed to fix glide androidX support
    implementation "com.android.support:support-annotations:28.0.0"
    annotationProcessor 'com.android.support:support-annotations:28.0.0'

    // the sample uses more advanced functions of the fastadapter
    // https://github.com/mikepenz/FastAdapter/
    implementation "com.mikepenz:fastadapter-extensions-utils:${versions.fastadapter}"
    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    implementation "com.mikepenz:aboutlibraries:${versions.aboutLibs}"

    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    implementation 'com.mikepenz:itemanimators:1.1.0'
    // used to provide the MiniDrawer to normal Drawer crossfade effect via a SlidingPane layout
    // --> https://github.com/mikepenz/MaterialDrawer/blob/develop/app/src/main/java/com/mikepenz/materialdrawer/app/MiniDrawerActivity.java
    // https://github.com/mikepenz/Crossfader
    implementation 'com.mikepenz:crossfader:1.6.0'
    // used to provide the two step crossfade DrawerLayout. Which allows to have a mini layout which transforms to a normal layout within the drawer
    // --> https://github.com/mikepenz/MaterialDrawer/blob/develop/app/src/main/java/com/mikepenz/materialdrawer/app/CrossfadeDrawerLayoutActvitiy.java
    // https://github.com/mikepenz/CrossfadeDrawerLayout
    implementation('com.mikepenz:crossfadedrawerlayout:1.1.0')

    // icon fonts used inside the sample
    // https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:google-material-typeface:3.0.1.4.original-kotlin@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.3.1.2-kotlin@aar'
    implementation 'com.mikepenz:octicons-typeface:3.2.0.6-kotlin@aar'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"
}

configurations.all {
    resolutionStrategy.force "com.mikepenz:iconics-core:${versions.iconics}"
    resolutionStrategy.force "com.mikepenz:fastadapter:${versions.fastadapter}"
}
