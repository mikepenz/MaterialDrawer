apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        setProperty("archivesBaseName", "MaterialDrawer-v$versionName-c$versionCode")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':library')

    implementation "com.google.android.material:material:${versions.androidX}"
    implementation "androidx.appcompat:appcompat:${versions.androidX}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidX}"
    implementation "androidx.cardview:cardview:${versions.androidX}"

    // used to showcase how to load images
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    // the sample uses more advanced functions of the fastadapter
    // https://github.com/mikepenz/FastAdapter/
    implementation 'com.mikepenz:fastadapter-commons:3.3.0-rc02'
    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    implementation 'com.mikepenz:aboutlibraries:6.2.0-rc02'

    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    implementation 'com.mikepenz:itemanimators:1.1.0-rc01'
    // used to provide the MiniDrawer to normal Drawer crossfade effect via a SlidingPane layout
    // --> https://github.com/mikepenz/MaterialDrawer/blob/develop/app/src/main/java/com/mikepenz/materialdrawer/app/MiniDrawerActivity.java
    // https://github.com/mikepenz/Crossfader
    implementation 'com.mikepenz:crossfader:1.6.0-rc01'
    // used to provide the two step crossfade DrawerLayout. Which allows to have a mini layout which transforms to a normal layout within the drawer
    // --> https://github.com/mikepenz/MaterialDrawer/blob/develop/app/src/main/java/com/mikepenz/materialdrawer/app/CrossfadeDrawerLayoutActvitiy.java
    // https://github.com/mikepenz/CrossfadeDrawerLayout
    implementation('com.mikepenz:crossfadedrawerlayout:1.1.0-rc01')

    // icon fonts used inside the sample
    // https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar'
    implementation 'com.mikepenz:octicons-typeface:3.0.0.1@aar'
}

configurations.all {
    resolutionStrategy.force "com.mikepenz:iconics-core:3.1.0-rc02"
    resolutionStrategy.force "com.mikepenz:fastadapter:3.3.0-rc02"
    resolutionStrategy.force "com.android.support:support-v4:${versions.androidX}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${versions.androidX}"
    resolutionStrategy.force "com.android.support:cardview-v7:${versions.androidX}"
    resolutionStrategy.force "com.android.support:recyclerview-v7:${versions.androidX}"
    resolutionStrategy.force "com.android.support:design:${versions.androidX}"
    resolutionStrategy.force "com.android.support:support-annotations:${versions.androidX}"
}
