apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
    print(ex.getMessage())
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        setProperty("archivesBaseName", "MaterialDrawer-v$versionName-c$versionCode")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
                print(ex.getMessage())
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
                print(ex.getMessage())
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':library')

    implementation "com.google.android.material:material:${versions.androidx}"
    implementation "androidx.appcompat:appcompat:${versions.androidx}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx}"
    implementation "androidx.cardview:cardview:${versions.androidx}"
    implementation "androidx.annotation:annotation:${versions.androidx}"

    // used to showcase how to load images
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.bumptech.glide:glide:4.8.0-SNAPSHOT'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0-SNAPSHOT'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0-SNAPSHOT@aar'
    implementation "com.android.support:support-annotations:28.0.0-alpha3"
    annotationProcessor 'com.android.support:support-annotations:28.0.0-alpha3'

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    implementation('com.mikepenz:aboutlibraries:6.0.9@aar') {
        transitive = true
    }
    //used to provide different itemAnimators for the RecyclerView
    implementation 'com.github.onlymash:recyclerview-itemanimators:1.0.2'
    // used to provide the MiniDrawer to normal Drawer crossfade effect via a SlidingPane layout
    // --> https://github.com/mikepenz/MaterialDrawer/blob/develop/app/src/main/java/com/mikepenz/materialdrawer/app/MiniDrawerActivity.java
    // https://github.com/mikepenz/Crossfader
    implementation 'com.mikepenz:crossfader:1.5.1@aar'

    // icon fonts used inside the sample
    // https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar'
    implementation 'com.mikepenz:octicons-typeface:3.0.0.1@aar'
}

configurations.all {
    resolutionStrategy.force "com.mikepenz:iconics-core:3.0.4"
}
